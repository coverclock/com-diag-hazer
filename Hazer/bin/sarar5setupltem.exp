#!/usr/bin/expect --
# Copyright 2022 Digital Aggregates Corporation, Colorado, USA
# Licensed under the terms in LICENSE.txt
# Chip Overclock <coverclock@diag.com>
# https://github.com/coverclock/com-diag-hazer
# Configures a U-blox SARA-R5 device using the serial connection
# to the cellular modem AT interface. This script uses the Diminuto
# serialtool utility to leverage its O_NONBLOCK openat(2).
# THIS IS A WORK IN PROGRESS.
# Usage: sarar5setupltem.exp [ DEVICE [ RATE [ PROVIDER [ PROTOCOL [ APN [ DNS1 [ DNS2 ] ] ] ] ] ] ]
# Defaults: sarar5setupltem.exp /dev/ttyUSB1 115200 2 IPV4V6 m2m64.com.attz 8.8.8.8 8.8.4.4

# Set defaults.

set device "/dev/ttyUSB1"
set rate "115200"
set provider "2"
set protocol "IPV4V6"
set apn "m2m64.com.attz"
set dns1 "8.8.8.8"
set dns2 "8.8.4.4"

# Set parameters.

set argc [ llength $argv ]
if { $argc > 0 } { set device   [ lindex $argv 0 ] }
if { $argc > 1 } { set rate     [ lindex $argv 1 ] }
if { $argc > 2 } { set provider [ lindex $argv 2 ] }
if { $argc > 3 } { set protocol [ lindex $argv 3 ] }
if { $argc > 4 } { set apn      [ lindex $argv 4 ] }
if { $argc > 5 } { set dsn1     [ lindex $argv 5 ] }
if { $argc > 6 } { set dns2     [ lindex $argv 6 ] }

# Prevent '\r' from being translated to '\n'.

set stty_init "raw"

send_user "Opening $device\n"
spawn serialtool -N -D $device -b $rate -8 -n -1

# Terminate any partial or invalid command entered prior.

send_user "Sending Carriage Return\n"
send "\r"

# Proceed with configuration including initial autobaud.

set commands [ list \
    "AT" \
    "AT" \
    "AT+CMEE=2" \
    "AT+CFUN=0" \
    "AT+UMNOPROF=$provider" \
    "AT+CGDCONT=1,\"$protocol\",\"$apn\"" \
    "AT+UPSD=1,0,2" \
    "AT+UPSD=1,1,\"$apn\"" \
    "AT+UPSD=1,4,\"$dns1\"" \
    "AT+UPSD=1,5,\"$dns2\"" \
    "AT+UPSD=1,100,1" \
    "AT+CFUN=1" \
    "AT+UPSDA=1,3" \
    "AT+CREG=1" \
]

set commandn [ llength $commands ]
set commandi 0
while { $commandi < $commandn } {
    set command [ lindex $commands $commandi ]
    set commandi [ expr $commandi + 1 ]
    set response "OK"
    send_user "Sending $command\n"
    send_user "Expecting $response\n"
    send "$command\r"
    expect {
        "+PACSP0" { send_user "Received +PACSP0\n"; exp_continue }
        "+PACSP1" { send_user "Received +PACSP1\n"; exp_continue }
        "$command" { send_user "Echoed\n"; exp_continue }
        timeout { send_user "Timeout\n"; exit 1 }
        "ERROR" { send_user "Failed\n"; exit 1 }
        "$response"
    }
    send_user "Received $response\n"
}

send_user "Rebooting\n"
send "AT+CFUN=16\r"

send_user "Closing\n"
close
exit 0
